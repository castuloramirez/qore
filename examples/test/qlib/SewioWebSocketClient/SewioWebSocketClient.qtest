#!/usr/bin/env qore
# -*- mode: qore; indent-tabs-mode: nil -*-

%new-style
%enable-all-warnings
%require-types
%strict-args

%requires ../../../../qlib/Util.qm
%requires ../../../../qlib/QUnit.qm
%requires ../../../../qlib/HttpServerUtil.qm
%requires ../../../../qlib/HttpServer.qm
%requires ../../../../qlib/WebSocketHandler.qm
%requires ../../../../qlib/WebSocketClient.qm
%requires ../../../../qlib/SewioWebSocketClient.qm

%exec-class SewioWebSocketClientTest

class MyWsConnection inherits WebSocketConnection {
    constructor(WebSocketHandler handler) : WebSocketConnection(handler) {
        map send($1), SewioWebSocketClientTest::Data;
    }
}

class MyWsHandler inherits WebSocketHandler {
    WebSocketConnection getConnectionImpl(hash cx, hash hdr, string cid) {
        return new MyWsConnection(self);
    }
}

class SewioWebSocketClientTest inherits QUnit::Test {
    public {
        HttpServer mServer;
        MyWsHandler wsHandler();
        int port;

        const Data = (
            "test1",
            <beeffeed>,
            "test3",
            );
    }

    constructor() : Test("SewioWebSocketClientTest", "1.0") {
        addTestCase("SewioWebSocketClient tests", \sewioWebSocketClientTests());

        mServer = new HttpServer(\log(), \log());
        mServer.setHandler("ws-handler", "", NOTHING, wsHandler);
        mServer.setDefaultHandler("ws-handler", wsHandler);
        port = mServer.addListener(0).port;

        set_return_value(main());

        mServer.stop();
    }

    sewioWebSocketClientTests() {
        list l = ();
        Counter c(1);
        code cb = sub (*data msg) {
            if (exists msg) {
                l += msg;
                if (l.size() == Data.size())
                    c.dec();
            }
        };
        SewioWebSocketClient wsc(cb, ("url": "sewiows://localhost:" + port, "apikey": "x"));
        wsc.connect();
        c.waitForZero();
        wsc.disconnect();
        delete wsc;

        assertEq(Data, l);
    }

    private log(string str) {
        if (m_options.verbose > 2)
            vprintf(str + "\n", argv);
    }
}
