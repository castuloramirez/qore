#!/usr/bin/env qore
#
# Take html chunks generated by docbook then create a PHP
# commentable scripts from it.
##

%require-types

const SRC = "qore-html";
const DST = "qore-php";

sub getPath(string $dir, string $fname) returns string {
    return $dir + "/" + $fname;
}

/*
TODO/FIXME: it does not work for this case. Dunno why, because it works for simpler inputs
sub iterateOverForA(reference $item) {
    #printf("iterateOverForA: %n\n", $item);
    if (type($item) == Type::Hash) {
        foreach my string $key in (keys $item) {
            if ($key == "a") {
                printf("A (pre): %N\n", $item.$key);
                if (!exists $item.$key."^value^") {
                    printf("        updating\n");
                    $item.$key."^value^" = "";
                }
                printf("A (upd): %N\n", $item.$key);
            }
            else
                iterateOverForA(\$item.$key);
        }
    }
    else if (type($item) == Type::List) {
        foreach my any $i in ($item)
            iterateOverForA(\$i);
    }
}
*/

sub fixA(string $in) returns string
{
    my int $offset = 0;
    my string $endTag = '"/>';
    my string $startTag = '<a id="';
    my string $newEndTag = '"></a>';

    while ($offset != -1) {
        $offset = index($in, $startTag, $offset);
        if ($offset == -1)
            break;
        my int $eoff = index($in, $endTag, $offset);
        my string $s = substr($in, $offset, $eoff-$offset+length($endTag));
#        printf("DEBUG: %n\n", $s);
        my string $newA = replace($s, $endTag, $newEndTag);
        $in = replace($in, $s, $newA);
        $offset += length($newA);
    }
    return $in;   
}


my Dir $d();
$d.chdir(SRC);

foreach my string $i in ($d.listFiles(".*\.html")) {
    printf("Processing: %s\n", $i);

    my File $f("UTF-8");
    $f.open2(getPath(SRC, $i));
    my string $xmlstr = $f.read(-1);
    $f.close();
    my hash $xml = parseXML($xmlstr);
    #printf("%N\n", $xml);
    if (! exists $xml.html.body.div || elements $xml.html.body.div == 0)
        throw "XHTML-STRUCTURE-ERROR", "No <div> list in xhtml.";

#$xml = ( "html" : ("body" : ( "a" :  ("foo" : "bar") )));
#    iterateOverForA(\$xml);

    $xml.html.head."^cdata" = "<?php include $_SERVER['DOCUMENT_ROOT'] . '/talkback/talkback/head-inc.php'; ?>";
    my hash $php;
    $php."^attributes^".class = "user_comments";
    $php."^cdata" = "<?php include $_SERVER['DOCUMENT_ROOT'] . '/talkback/talkback/comments.php'; ?>";
    push $xml.html.body.div, $php;

#    printf("\n\n%N\n", $xml);

    my string $html = makeXMLString($xml);
    $html = replace($html, "<![CDATA[<?php include $_SERVER['DOCUMENT_ROOT'] . '/talkback/talkback/head-inc.php'; ?>]]>",
                           "<?php include $_SERVER['DOCUMENT_ROOT'] . '/talkback/talkback/head-inc.php'; ?>");
    $html = replace($html, "<![CDATA[<?php include $_SERVER['DOCUMENT_ROOT'] . '/talkback/talkback/comments.php'; ?>]]>",
                           "<?php include $_SERVER['DOCUMENT_ROOT'] . '/talkback/talkback/comments.php'; ?>");

    $html = fixA($html);
    $f.open2(getPath(DST, $i), O_CREAT | O_WRONLY);
    $f.write($html);
    $f.close();
}

