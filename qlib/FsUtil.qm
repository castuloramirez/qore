# -*- mode: qore; indent-tabs-mode: nil -*-
# @file FsUtil.qm Qore user module providing file system related functionality

/*  FsUtil.qm Copyright 2017 Qore Technologies, s.r.o.

    Permission is hereby granted, free of charge, to any person obtaining a
    copy of this software and associated documentation files (the "Software"),
    to deal in the Software without restriction, including without limitation
    the rights to use, copy, modify, merge, publish, distribute, sublicense,
    and/or sell copies of the Software, and to permit persons to whom the
    Software is furnished to do so, subject to the following conditions:

    The above copyright notice and this permission notice shall be included in
    all copies or substantial portions of the Software.

    THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
    IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
    FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
    AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
    LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING
    FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER
    DEALINGS IN THE SOFTWARE.
*/

# minimum required Qore version
%requires qore >= 0.8.13

# assume local var scope, do not use "$" for vars, members, and method calls
%new-style
%strict-args
%require-types
%enable-all-warnings

module FsUtil {
    version = "1.0";
    desc = "user module providing file system related functionality";
    author = "Tomas Heger";
    url = "http://qore.org";
    license = "MIT";
    init    = sub () { init(); };
}

/* see release notes below for version history
*/

/** @mainpage FsUtil Module

    @tableofcontents

    @section fsutilintro Introduction to the FsUtil Module

    The %FsUtil module provides functionality for file system operations.

    To use this module, use \c "%requires FsUtil" in your code.

    All the public symbols in the module are defined in the FsUtil namespace

    Currently the module provides the following multiplatform functions:
    @ref FsUtil::basename()
    @ref FsUtil::join_paths()

    Also classes for platform specific operations are available:
    @ref FsUtil::PosixPathHandler
    @ref FsUtil::WinPathHandler


    @section fsutil_relnotes Release Notes

    @subsection fsutil_v1_0 Version 1.0
    - initial version of module
*/

namespace Init {

    sub init() {
        #! handler for the current platform
        our FsUtil::PathHandler local_handler;
        if (Platform.Windows)
            local_handler = new FsUtil::WinPathHandler();
        else if (Platform.Unix)
            local_handler = new FsUtil::PosixPathHandler();
        else
            throw "UNKNOWN-PLATFORM-ERROR",
                  sprintf("Unknown platform: %s.\n", PlatformOS);
    }
}

#! the FsUtil namespace contains all the objects in the FsUtil module
public namespace FsUtil {

    #! generic path handler implementing functionality common for both platforms
    class PathHandler {
        public {
            #! path delimiters - to be set by platform specific path handlers
            list Delimiters;
        }

        #! returns the segment of given path after the last delimiter
        /** @param path the path to be processed

            @return everything after the last delimiter

            Delimiters for the current platform are used.
        */
        public string basename(string path) {
            string tail = path;
            foreach string delim in (self.Delimiters) {
                int index = tail.rfind(delim) + 1;
                tail = tail.substr(index);
            }
            return tail;
        }

        # to be implemented by platform-specific classes
        public abstract string join_paths(string path1, string path2);
        #public string join(string path1, string path2) {
        #    throw "NOT-IMPLEMENTED-ERROR", "child classes should implement it";
        #}
    }

    #! path handler implementing Windows specific functionality
    public class WinPathHandler inherits PathHandler {

        #! creates the object and sets the delimiter
        constructor() {
            # both slashes can be used as path delimiters on Windows
            Delimiters = ("\\", "/");
        }

        #! separates and returns the drive and the rest of the path
        /** @param path the path to be processed

            @return list of two items - drive and the rest of the path, either
            can be empty and concatenation of both parts always gives the
            original path back.
        */
        public list split_drive(string path) {
            if (regex(path, '^[a-zA-Z]:'))
                return (path.substr(0,2), path.substr(2));
            return ("", path);
        }

        #! returns the segment of given path after the last delimiter
        /** @param path the path to be processed

            @return everything after the last delimiter

            Delimiter for the current platform is used.
        */
        public string basename(string path) {
            # get rid of the drive first (if present)
            string tail = self.split_drive(path)[1];
            # find the last segment of the rest
            foreach string delim in (self.Delimiters) {
                int index = tail.rfind(delim) + 1;
                tail = tail.substr(index);
            }
            return tail;
        }

        #! Returns path resulting from joining the given paths
        /** @param path1 the first part of the path
            @param path2 the second part of the path

            @return the two paths joined into one valid path (i.e. delimiters
            are added/removed as needed) except for cases when path2 is an
            absolute path or if each path is on different drive - in such cases
            path2 is returned as the result. Otherwise the resulting path is not
            normalized (i.e. "/path/to/../../file.txt" is a correct result of
            join("/path/to", "../../file.txt");).
        */
        public string join_paths(string path1, string path2) {
            if (path2 == "")
                return path1;

            # if the first path is empty or if the second path is absolute or
            # if both paths contain drive and the drives differ, return path2
            list path1_split = split_drive(path1);
            list path2_split = split_drive(path2);
            if (path1 == "" ||
                absolute_path_windows(path2) ||
                (path2_split[0] != "" &&
                 toupper(path1_split[0]) != toupper(path2_split[0])))
                return path2;

            # otherwise let's join both parts and return
            if (path1_split[1] != "" && !inlist(path1[-1], self.Delimiters))
                path1 += self.Delimiters[0];
            return path1 + path2_split[1];
        }
    }

    #! path handler implementing POSIX specific functionality
    public class PosixPathHandler inherits PathHandler {

        #! creates the object and sets the delimiter
        constructor() {
            Delimiters = ("/",);
        }

        #! Returns path resulting from joining the given paths
        /** @param path1 the first part of the path
            @param path2 the second part of the path

            @return the two paths joined into one valid path (i.e. delimiters
            are added/removed as needed) except for the case when path2 is an
            absolute path - in such case path2 is returned as the result.
            Otherwise the resulting path is not normalized (i.e.
            "/path/to/../../file.txt" is a correct result of
            join("/path/to", "../../file.txt");).
        */
        public string join_paths(string path1, string path2) {
            if (path2 == "") {
                return path1;
            }

            # if the second path is absolute or if both paths contain drive and
            # the drives differ, return path2
            if (absolute_path_unix(path2)) {
                return path2;
            }

            if (path1 != "" && path1[-1] != self.Delimiters[0]) {
                path1 += self.Delimiters[0];
            }
            return (path1 + path2);
        }
    }

    #! this function returns the segment of given path after the last delimiter
    /** @param path the path to be processed

        @return the last part of the path - everything after the last delimiter

        Delimiters for the current platform are used.
    */
    public string sub basename(string path) {
        return local_handler.basename(path);
    }

    #! Returns path resulting from joining the given paths
    /** @param path1 the first part of the path
        @param path2 the second part of the path

        @return the two paths joined into one valid path (i.e. delimiters are
        added/removed as needed) except for cases when path2 is an absolute path
        or if each path is on different drive (Windows only) - in such cases
        path2 is returned as the result. Otherwise the resulting path is not
        normalized (i.e. "/path/to/../../file.txt" is a correct result of
        join("/path/to", "../../file.txt");).
    */
    public string sub join_paths(string path1, string path2) {
        return local_handler.join_paths(path1, path2);
    }

} # FsUtil namespace
