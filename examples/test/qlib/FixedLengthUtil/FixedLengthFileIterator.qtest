#!/usr/bin/env qore
%require-types
%new-style
%enable-all-warnings
%requires UnitTest
%requires ../../../../qlib/FixedLengthUtil.qm

UnitTest unit();

string file = unit.tmpLocation() + "/file.fll";
unlink(file);
File FW = new File();
FW.open(file, O_WRONLY | O_CREAT);
FW.write("11111bb\ncddd31122014\n\n\n22222gg\n");
FW.close();

hash specs = (
    "type1" : (
        "col1" : (
            "length" : 5,
            "type"   : "int"
        ),
        "col2" : (
            "length" : 2,
            "type"   : "string",
        ),
    ),
    "type2" : (
        "col3" : (
            "length" : 1,
            "type"   : "string",
        ),
        "col4" : (
            "length" : 3,
            "type"   : "string",
        ),
        "col5" : (
            "length"   : 8,
            "type"     : "date",
            "format"   : "DDMMYYYY",
#                "timezone" : "Europe/Prague", # use global if omitted
        ),
    ),
);

hash global_options = (
    "encoding"     : "UTF-8",
    "eol"          : "\n",
    "ignore-empty" : True,
    "timezone"     : "Europe/Prague", # used if not in some date column specification
);

FixedLengthFileIterator i(file, specs, global_options);
unit.cmp(i.next(), True, "line 1 is there");
unit.cmp(i.getValue(), ("type": "type1", "record": ("col1" : 11111, "col2" : "bb")), "line 1 content check");
unit.cmp(i.next(), True, "line 2 is there");
unit.cmp(i.getValue(), ("type": "type2", "record": ("col3" : "c", "col4" : "ddd", "col5" : 2014-12-31)), "line 2 content check");
unit.cmp(i.next(), True, "line 3 is there");
unit.cmp(i.getValue(), ("type": "type1", "record": ("col1" : 22222, "col2" : "gg")), "line 3 content check");
unit.cmp(i.next(), False, "line 4 is not there");

unlink(file);
