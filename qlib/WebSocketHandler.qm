# -*- mode: qore; indent-tabs-mode: nil -*-
#! @file WebSocketHandler.qm Qore handler definition for the HttpServer module for providing WebSocket server services

/*  WebSocketHandler.qm Copyright 2013 Qore Technologies, sro

    Permission is hereby granted, free of charge, to any person obtaining a
    copy of this software and associated documentation files (the "Software"),
    to deal in the Software without restriction, including without limitation
    the rights to use, copy, modify, merge, publish, distribute, sublicense,
    and/or sell copies of the Software, and to permit persons to whom the
    Software is furnished to do so, subject to the following conditions:

    The above copyright notice and this permission notice shall be included in
    all copies or substantial portions of the Software.

    THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
    IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
    FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
    AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
    LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING
    FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER
    DEALINGS IN THE SOFTWARE.
*/

# this module requires Qore 0.8.8 or better
%requires qore >= 0.8.8

# require type definitions everywhere
%require-types

# enable all warnings
%enable-all-warnings

%requires HttpServer >= 0.3.7

module WebSocketHandler {
    version = "1.0";
    desc = "user module for providing WebSocket server services";
    author = "David Nichols <david@qore.org>";
    url = "http://qore.org";
}

/*  Version History
    * 2013-xx-xx v1.0: David Nichols <david@qore.org>
      + the initial version of the WebSocketHandler module
*/

/** @mainpage WebSocketHandler Module

    The %WebSocketHandler module provides a handler to be used with the <a href="../../HttpServer/html/index.html">HttpServer module</a> to
    provide server-side <a href="http://tools.ietf.org/html/rfc6455">RFC-6455 based WebSocket services</a> to clients.

    The main classes provided in this module are:
    - @ref WebSocketHandler::WebSocketConnection
    - @ref WebSocketHandler::WebSocketHandler
*/

#! the WebSocketHandler namespace contains all the objects in the WebSocketHandler module
public namespace WebSocketHandler {
    #! the class handling a dedicated connection
    public class WebSocketConnection {
        public {
            Queue $.queue();
            Socket $.sock;

            WebSocketHandler $.handler;

            const DefaultQueuePollingInterval = 50ms;
        }

        constructor(WebSocketHandler $handler, Socket $s) {
            $.sock = $s;
            $.handler = $handler;
        }

        *data pollData() {
            try {
                return $.queue.get(DefaultQueuePollingInterval);
            }
            catch (hash $ex) {
                if ($ex.err != "QUEUE-TIMEOUT")
                    rethrow;
            }
        }

        gotMessage(string $msg) {
        }

        gotMessage(binary $msg) {
        }
    }

    #! the main web socket handler class
    public class WebSocketHandler inherits AbstractHttpSocketHandler {
        public {
            #! WebSocket GUID
            const GUID = "258EAFA5-E914-47DA-95CA-C5AB0DC85B11";

            #! the final fragment in a message
            const FIN = (1 << 7); 

            /** @defgroup opcodes WebSocket OpCodes
             */
            #@{
            #! continuation frame opcode
            const Op_Continuation = 0x0;

            #! text frame opcode
            const Op_Text = 0x1;

            #! binary frame opcode
            const Op_Binary = 0x2;

            #! connection code opcode
            const Op_Close = 0x8;

            #! ping opcode
            const Op_Ping = 0x9;

            #! pong opcode
            const Op_Pong = 0xa;

            #! maps from opcodes to text descriptions
            const OpMap = (
                Op_Continuation: "CONTINUATION",
                Op_Text: "TEXT",
                Op_Binary: "BINARY",
                Op_Close: "CLOSE",
                Op_Ping: "PING",
                Op_Pong: "PONG",
                );
            #@}

            /** @defgroup closecodes WebSocket Close Codes
             */
            #@{
            #! "Normal Closure" code
            const CC_NormalClosure = 1000;

            #! "Going Away" code
            const CC_GoingAway = 1001;

            #! "Protocol Error" code
            const CC_ProtocolError = 1002;

            #! "Unsupported Data" code
            const CC_UnsupportedData = 1003;

            #! "No Status Rcvd" code
            const CC_NoStatusRcvd = 1005;

            #! "Abnormal Closure" code
            const CC_AbnormalClosure = 1006;

            #! "Invalid Frame Payload Data" code
            const CC_InvalidData = 1007;

            #! "Policy Violation" code
            const CC_PolicyViolation = 1008;

            #! "Message Too Big" code
            const CC_MessageTooBig = 1009;

            #! "Mandatory Ext." code
            const CC_MandatoryExt = 1010;

            #! "Internal Server Error" code
            const CC_InternalServerError = 1011;

            #! "TLS Handshake" code
            const CC_TlsHandshake = 1015;

            #! maps from close codes to text descriptions
            const CcMap = (
                CC_NormalClosure: "Normal Closure",
                CC_GoingAway: "Going Away",
                CC_ProtocolError: "Protocol Error",
                CC_UnsupportedData: "Unsupported Data",
                CC_NoStatusRcvd: "No Status Rcvd",
                CC_AbnormalClosure: "Abnormal Closure",
                CC_InvalidData: "Invalid Frame Payload Data",
                CC_PolicyViolation: "Policy Violation",
                CC_MessageTooBig: "Message Too Big",
                CC_MandatoryExt: "Mandatory Ext.",
                CC_InternalServerError: "Internal Server Error",
                CC_TlsHandshake: "TLS Handshake",
                );
            #@}
        }

        private {
            #! stop listener flag
            bool $.stop = False;

            #! connection read-write lock
            RWLock $.rwl();

            #! connection hash
            hash $.ch;            
        }

        #! create the object optionally with the given AbstractAuthenticator
        /** @param auth the authentication object to use to authenticate connections (see AbstractAuthenticator); if no AbstractAuthenticator object is passed, then by default no authentication will be required
         */
        constructor(*AbstractAuthenticator $auth) : AbstractHttpSocketHandler($auth) {
        }

        #! called by the HTTP server to handle incoming HTTP requests
        /** Requests will be dispatched to REST classes registered with this object

            @param cx call context hash; this hash will have the following keys:
            - \c socket: the bind address used to bind the listener (\c "socket-info" provides more detailed information)
            - \c socket-info: a hash of socket information for the listening socket (as returned by Qore::Socket::getSocketInfo())
            - \c peer-info: a hash of socket information for the remote socket (as returned by Qore::Socket::getPeerInfo())
            - \c url: a hash of broken-down URL information (as returned from parseURL())
            - \c id: the unique HTTP connection ID
            - \c listener-id: the HTTP server listener ID (see HttpServer::getListenerInfo())
            - \c user: the current RBAC username (if any)
            @param hdr incoming header hash; all keys will be converted to lower-case, additionally the following keys will be present:
            - \c method: the HTTP method received (ie \c "GET", \c "POST", etc)
            - \c path: the HTTP path given in the request, after processing by decode_url() (Qore function)
            - \c http_version: the HTTP version number in the request (either \c "1.0" or \c "1.1")
            @param b message body, if any

            @return a hash representing the response to the client as follows; to accept a dedicated connection; make sure the \c "code" is 101 (ie \c "Switching Protocols") and the \c "close" key is not @ref Qore::False "False":
            - \c "code": the HTTP return code (see @ref HttpServer::HttpCodes) (101 \c "Switching Protocols" to accept the dedicated connection, in which case the start() method will be called)
            - \c "body": the message body to return in the response
            - \c "close": (optional) set this key to @ref Qore::True "True" if the connection should be unconditionally closed when the handler returns
            - \c "hdr": (optional) set this key to a hash of extra header information to be returned with the response
         */
        hash handleRequest(hash $cx, hash $hdr, *data $b) {
            #$.logDebug("websocket request: cx: %N", $cx);
            #$.logDebug("websocket request: hdr: %N", $hdr);
            if (!$hdr."sec-websocket-key")
                return (
                    "code": 400,
                    "body": "cannot open WebSocket connection; no Sec-WebSocket-Key header found in request",
                );

            my string $key = $hdr."sec-websocket-key";
            my string $response_key = makeBase64String(SHA1_bin($key + GUID));

            my hash $rhdr = (
                "Upgrade": "websocket",
                "Connection": "Upgrade",
                "Sec-WebSocket-Accept": $response_key,
                );

            return (
                "code": 101,
                "hdr": $rhdr,
                );
        }

        #! called from the HTTP server after the handleRequest() method indicates that a dedicated connection should be established
        /** This method should not return until the connection is closed or the stop() method is called

            @param cx call context hash; this hash will have the following keys:
            - \c socket: the bind address used to bind the listener (\c "socket-info" provides more detailed information)
            - \c socket-info: a hash of socket information for the listening socket (as returned by Qore::Socket::getSocketInfo())
            - \c peer-info: a hash of socket information for the remote socket (as returned by Qore::Socket::getPeerInfo())
            - \c url: a hash of broken-down URL information (as returned from parseURL())
            - \c id: the unique HTTP connection ID
            - \c listener-id: the HTTP server listener ID (see HttpServer::getListenerInfo())
            - \c user: the current RBAC username (if any)
            @param sock the @ref Qore::Socket "Socket" object for the dedicated connection to the client
         */
        start(hash $cx, Socket $sock) {
            $.logDebug("WebSocketHandler::start() cid: %y from %s:%d", $cx.id, $cx."peer-info".hostname_desc, $cx."peer-info".port);

            my softstring $cid = $cx.id;

            # get new connection object
            my WebSocketConnection $wsc = $.getConnectionImpl($cid, $sock);

            # add connection object to hash
            {
                $.rwl.writeLock();
                on_exit $.rwl.writeUnlock();
                $.ch.$cid = $wsc;
            }

            on_exit {
                $.rwl.writeLock();
                on_exit $.rwl.writeUnlock();
                delete $.ch.$cid;
            }

            while (True) {
                if ($.stop) {
                    $.sendClose($sock, CC_GoingAway);
                    break;
                }

                try {
                    # send any messages on the queue
                    my *data $d = $wsc.pollData();
                    if ($d)
                        $sock.send(WebSocketHandler::encodeMessage($d));

                    my int $b0;
                    try {
                        $b0 = $sock.recvu1(1ms);
                    }
                    catch (hash $ex) {
                        if ($ex.err == "SOCKET-CLOSED") {
                            return;
                        }
                        if ($ex.err != "SOCKET-TIMEOUT")
                            printf("%s: %s\n", $ex.err, $ex.desc);
                        continue;
                    }

                    # get opcode
                    my int $op = $b0 & 0xf;
                    
                    # get next byte
                    my int $b1 = $sock.recvu1();
                    my softbool $masked = $b1 & 0x80;
                    $b1 &= 0x7f;
                    
                    my int $data_len;
                    if ($b1 < 126)
                        $data_len = $b1;
                    else if ($b1 < 65535)
                        $data_len = $sock.recvu2();
                    else
                        $data_len = $sock.recvi8();
                    
                    my *binary $mask;
                    if ($masked) {
                        $mask = $sock.recvBinary(4);
                        $.logDebug("mask: %s", $mask.toHex());
                    }
                    
                    my *data $data;
                    if ($data_len)
                        $data = $sock.recvBinary($data_len);
                    
                    # unmask data if necessary
                    if ($mask) {
                        # unmasked data
                        my binary $udata;
                        for (my int $i = 0; $i < $data.size(); ++$i)
                            $udata += chr(($data[$i] ^ $mask[$i % 4]));
                        $data = $udata;
                    }

                    if ($op == Op_Close) {
                        if (!$data) {
                            $.logDebug("client sent OpClose without code; closing immediately");
                            break;
                        }
                        my int $code = get_word_32($data, 0);
                        # remove the first 4 bytes extracted above
                        splice $data, 0, 4;
                        my *string $txtmsg = $data ? $data.toString("utf8") : NOTHING;
                        $.logDebug("client sent Op_Close code %d (%s): %s", $code, CcMap.$code, $txtmsg);
                        $.sendClose($sock, $code, $txtmsg);
                        break;
                    }

                    if ($op == Op_Text)
                        $data = $data.toString($sock.getEncoding());

                    $.logDebug("received msg (%s: %d bytes%s): %s", OpMap.$op, $data.size(), $masked ? " masked" : "", WebSocketHandler::getDataString($data));

                    if (!$masked) {
                        $.logError("unmasked client frame received");
                        $.sendClose($sock, CC_ProtocolError);
                        break;
                    }
                    
                    if ($op == Op_Ping) {
                        $sock.send(WebSocketHandler::encodeMessage($data, Op_Pong));
                        continue;
                    }
                    
                    if ($op == Op_Text || $op == Op_Binary) {
                        $wsc.gotMessage($data);
                        continue;
                    }
                    # unrecognized opcode - close the connection
                    $.sendClose($sock, CC_UnsupportedData);
                    break;
                }
                catch (hash $ex) {
                    # error in websocket handler server, log and close the connection
                    my string $err = sprintf("%s: %s: %s", get_ex_pos($ex), $ex.err, $ex.desc);
                    $.logError($err);
                    $.sendClose($sock, CC_InternalServerError, $err);
                    break;
                }
            }

            $.logDebug("WebSocketHandler::start() exiting");
        }

        WebSocketConnection getConnectionImpl(string $cid, Socket $sock) {
            return new WebSocketConnection($self, $sock);
        }

        sendAll(data $d) {
            $.rwl.readLock();
            on_exit $.rwl.readUnlock();

            # push the data on all connection queues
            map $1.queue.push($d), $.ch.iterator();

            #if ($.ch) $.logDebug("sending data to connections: %y", $.ch.keys());
        }

        sendOne(string $id, data $d) {
            $.rwl.readLock();
            on_exit $.rwl.readUnlock();

            if (!$.ch.$id)
                throw "WEBSOCKET-CONNECTION-ERROR", sprintf("connection %y does not exist (valid connections: %y)", $id, $.ch.keys());

            $.ch.$id.queue.push($d);
        }

        private sendClose(Socket $sock, int $code, *string $txtmsg) {
            my binary $msg = $code.encodeMsb(4);

            if (!CcMap.$code) {
                $.logError("WebSocketHandler::sendClose(): invalid close code %d received; expecting one of: %y", $code, (map $1.toInt(), CcMap.keyIterator()));
                $code = CC_InternalServerError;
            }

            if ($txtmsg)
                $txtmsg = convert_encoding($txtmsg, "utf8");
            else
                $txtmsg = CcMap.$code;
            $msg += $txtmsg;
            $sock.send(WebSocketHandler::encodeMessage($msg, Op_Close));
        }

        static string getDataString(*data $data) {
            if (!$data)
                return "n/a";
            return sprintf("type: %s: %y", $data.type(), $data);
            /*
            if ($data.typeCode() == NT_STRING)
                return $data;
            return $data.toHex();
            */
        }

        private static binary encodeMessage(data $msg, int $op = -1) {
            if ($op == -1)
                $op = $msg.typeCode() == NT_STRING ? Op_Text : Op_Binary;
            my binary $frame += chr(FIN | $op);
            
            # encode frames with a message size < 126 directly in the second byte
            switch ($msg.size()) {
                case < 126: {
                    $frame += chr($msg.size());
                    break;
                }
                case < 65536: {
                    $frame += chr(126);
                    $frame += $msg.size().encodeMsb(2);
                    break;
                }
              default: {
                  $frame += chr(127);
                  $frame += $msg.size().encodeMsb(8);
                }
            }
            $frame += $msg;
            
            return $frame;
        }

        #! called from the HTTP server when the socket should be closed due to an external request; the start() method should return as soon as possible after this method is called
        stop() {
            $.logDebug("external stop called");
            $.stop = True;
        }

        #! default implementation is empty
        logInfo(string $fmt) {
            # to avoid warnings about extra arguments in the call
            delete $argv;
        }

        #! default implementation is empty
        logError(string $fmt) {
            # to avoid warnings about extra arguments in the call
            delete $argv;
        }

        #! default implementation is empty
        logDebug(string $fmt) {
            # to avoid warnings about extra arguments in the call
            delete $argv;
        }
    }
}
