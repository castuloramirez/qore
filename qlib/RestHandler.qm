# -*- mode: qore; indent-tabs-mode: nil -*-
# @file RestHandler.qm Qore user module for working with SQL data

/*  RestHandler.qm Copyright 2013 Qore Technologies, sro

    Permission is hereby granted, free of charge, to any person obtaining a
    copy of this software and associated documentation files (the "Software"),
    to deal in the Software without restriction, including without limitation
    the rights to use, copy, modify, merge, publish, distribute, sublicense,
    and/or sell copies of the Software, and to permit persons to whom the
    Software is furnished to do so, subject to the following conditions:

    The above copyright notice and this permission notice shall be included in
    all copies or substantial portions of the Software.

    THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
    IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
    FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
    AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
    LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING
    FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER
    DEALINGS IN THE SOFTWARE.
*/

# this module requires Qore 0.8.8 or better
%requires qore >= 0.8.8

# require type definitions everywhere
%require-types

# enable all warnings
%enable-all-warnings

%requires HttpServer >= 0.3.7
%requires Mime >= 1.3

%try-module yaml
%define NoYaml
%endtry

%try-module xml
%define NoXml
%endtry

%try-module json
%define NoJson
%endtry

module RestHandler {
    version = "0.1";
    desc = "user module for implementing REST services with the Qore HTTP server";
    author = "David Nichols <david@qore.org>";
    url = "http://qore.org";
}

/** @mainpage RestHandler Module

    The %RestHandler module provides functionality for implementing REST services with the Qore HTTP server.

    To use this module, use \c "%requires RestHandler" in your code.
    
    All the public symbols in the module are defined in the RestHandler namespace
*/

#! the RestHandler namespace contains all the objects in the RestHandler module
public namespace RestHandler {
    public class AbstractRestClass {
        public {}

        constructor() {
        }

        abstract string name();

        *AbstractRestClass subClass(string $name) {
        }

        hash handleRequest(RestHandler $rh, *list $cl, string $mn, *hash $args, hash $cx) {            
            if ($cl) {
                my string $cname = shift $cl;
                my *AbstractRestClass $cls = $.subClass($cname);
                if (!$cls) {
                    $rh.logDebug("unknown subclass %y: class %y: method %y args: %y", $cname, $.name(), $mn, $args);
                    return $.unknownSubClassError($cname);
                }

                $rh.logDebug("class %y: dispatching to subclass class %y: method %y args: %y", $.name(), $cname, $mn, $args);
                return $cls.handleRequest($rh, $cl, $mn, $args, $cx);
            }
            
            $rh.logDebug("class %y: dispatching method %y args: %y", $.name(), $mn, $args);
            return $.dispatch($rh, NOTHING, $mn, $args, $cx);
        }
        
        hash dispatch(RestHandler $rh, *string $oname, string $mn, *hash $ah, hash $cx) {
            try {
                return callObjectMethodArgs($self, $mn, ($cx, $ah));
            }
            catch (hash $ex) {
                if ($ex.err == "METHOD-DOES-NOT-EXIST") {
                    $rh.logDebug("%s: class %y: method %y args: %y", $ex.err, $.name(), $mn, $ah);
                    return RestHandler::make501("REST class %y does not implement method %y", $.name(), $mn);
                }
                $rh.logDebug("class %y: method %y: %s: %s: %s", $.name(), $mn, get_ex_pos($ex), $ex.err, $ex.desc);
                rethrow;
            }
        }
        
        hash unknownSubClassError(string $name) {
            return RestHandler::make400("class %y has no subclass %y", $.name(), $name);
        }
        
        hash post(hash $cx, *hash $ah) {
            return RestHandler::Err501;
        }
        
        hash get(hash $cx, *hash $ah) {
            return RestHandler::Err501;
        }
        
        hash del(hash $cx, *hash $ah) {
            return RestHandler::Err501;
        }

        hash put(hash $cx, *hash $ah) {
            return RestHandler::Err501;
        }
    }

    public class RestHandler inherits AbstractHttpRequestHandler {
        public {
            const Err501 = (
                "code": 501,
                "body": "not implemented",
                );

            #! supported HTTP methods
            const Methods = (
                "GET": True,
                "POST": True,
                "PUT": True,
                "DELETE": True,
                );

            #! supported mime types for de/serializing responses
            const MimeDataTypes = (
%ifndef NoYaml
                MimeTypeYaml: True,
                MimeTypeYamlRpc: True,
%endif
%ifndef NoXml
                MimeTypeXml: True,
%endif
%ifndef NoJson
                MimeTypeJsonRpc: True,
%endif
                );
        }
        
        private {
            #! class hash: name -> AbstractRestClass
            hash $.ch;

            #! mutex lock
            Mutex $.mx();
        }
        
        constructor(*AbstractAuthenticator $auth) : AbstractHttpRequestHandler($auth) {
        }

        #! handles the HTTP requests
        hash handleRequest(hash $cx, hash $hdr, *data $b) {
            #$.logDebug("cx: %N", $cx);
            $.logDebug("hdr: %N", $hdr);
            $.logDebug("body: %s", $b);

            my any $body = $b;
            
            # get rest class name
            my string $path = $cx.url.path;

            $.removeRootPath(\$path);

            my *string $cls = ($path =~ x/^([^\/?]+)/)[0];
            if (!$cls)
                return RestHandler::make400("cannot find REST class in path %y", $cx.url.path);
            if (!$.ch{$cls})
                return RestHandler::make400("unknown REST class %y in path %y (valid classes: %y)", $cls, $cx.url.path, $.ch.keys());

            # check method
            if (!Methods.($hdr.method))
                return RestHandler::make501("cannot handle HTTP method %y; valid methods: %y", $hdr.method, Methods.keys());
            
            # remove class and trailing "/" from path
            splice $path, 0, $cls.size();
            if ($path[0] == "/")
                splice $path, 0, 1;

            # get object and args
            my hash $ah = parse_uri_query($path);

            my *hash $args;
            if (exists $ah.params[0]) {
                if ($ah.params[0].typeCode() != NT_HASH)
                    return RestHandler::make400("cannot parse arguments in REST call %y, the first argument must be a hash with an 'action' key; got type %y instead", $path, $ah.params[0].type());
                if ($ah.params.size() > 1)
                    return RestHandler::make400("invalid arguments in REST call %y, only a single hash argument is accepted; got %y instead", $path, $ah.params);
                $args = $ah.params[0];
            }

            # deserialize body according to Content-Type
            if ($body) {
                my bool $decoded = False;
%ifndef NoYaml
                # check for yaml support
                foreach my string $ct in (MimeTypeYaml, MimeTypeYamlRpc) {
                    if ($hdr."content-type" == $ct) {
                        $body = parseYAML($body);
                        $decoded = True;
                        break;
                    }
                }
%endif
%ifndef NoXml
                # check for xml-rpc support
                if (!$decoded && $hdr."content-type" == MimeTypeXml) {
                    $body = parseXMLRPCValue($body);
                    $decoded = True;
                }
%endif
%ifndef NoJson
                # check for json support
                if (!$decoded && $hdr."content-type" == MimeTypeJsonRpc) {
                    $body = parseJSON($body);
                    $decoded = True;
                }
%endif
                if (!$decoded) {
                    $.requestDeserializationError($hdr, $cx, $body);
                    return RestHandler::make501("cannot deserialize request body; content-type is: %y; types supported: %y; discarding request body: %y", $hdr."content-type", MimeDataTypes.keys(), $body);
                }

                # add any hash body to query arguments for easier processing if the HTTP method is not GET
                # and comliance with lazy clients that put arguments in the URI with non GET methods
                if ($hdr.method != "GET" && $body.typeCode() == NT_HASH)
                    $args += $body;
            }
            else
                $body = $args;
            
            # make method name
            my string $mn;
            if ($args.action) {
                if ($args.action.typeCode() != NT_STRING)
                    return RestHandler::make400("cannot parse arguments in REST call %y, the 'action' key is not assigned to a string; got %y instead", $path, $args.action.type());
                $mn = $args.action;
                $args -= "action";
                # ensure first letter of the action is in upper case
                splice $mn, 0, 1, $mn[0].upr();
                $mn = $hdr.method.lwr() + $mn;
            }
            else
                $mn = $hdr.method.lwr();

            # get class chain
            my *list $cl = $ah.method ? $ah.method.split("/") : NOTHING;

            # handle call with class
            my hash $rv = $.ch{$cls}.handleRequest($self, $cl, $mn, $args, $cx + ("hdr": $hdr, "body": $body));

            # serialize response according to client's capabilities if necessary
            if (exists $rv.body) {
                if ($rv.body.typeCode() != NT_STRING) {
                    # make a hash of "accept" values
                    my *string $astr = $hdr.accept;
                    my hash $aih;
                    if ($astr) {
                        $astr =~ s/;.*//;#/;
                        map $aih.$1 = True, $astr.split(",");
                    }

                    my bool $encoded;
%ifndef NoYaml
                    # check for yaml support
                    foreach my string $ct in (MimeTypeYaml, MimeTypeYamlRpc) {
                        if ($aih.$ct || $aih."*/*") {
                            $rv.body = makeYAML($rv.body);
                            $rv.hdr = $ct;
                            $encoded = True;
                            break;
                        }
                    }
%endif
%ifndef NoXml
                    # check for xml-rpc support
                    if (!$encoded && ($aih{MimeTypeXml} || $aih."*/*")) {
                        $rv.body = makeXMLRPCValueString($rv.body);
                        $rv.hdr = MimeTypeXml;
                        $encoded = True;
                    }
%endif
%ifndef NoJson
                    # check for json support
                    if (!$encoded && ($aih{MimeTypeJsonRpc} || $aih."*/*")) {
                        $rv.body = makeJSONString($rv.body);
                        $rv.hdr = MimeTypeJsonRpc;
                        $encoded = True;
                    }
%endif
                    if (!$encoded) {
                        $.responseSerializationError($cx, $aih, $rv);
                        return RestHandler::make501("cannot serialize response; you claim to accept only: %y; types supported: %y; discarding response: %y", $aih.keys(), MimeDataTypes.keys(), $rv.body);
                    }
                }
            }
            else $rv.body = "OK";

            return $rv;
        }

        #! default implementation is empty
        removeRootPath(reference $path) {
        }
        
        #! default implementation is empty
        requestDeserializationError(hash $hdr, hash $cx, string $body) {
        }

        #! default implementation is empty
        responseSerializationError(hash $cx, *hash $aih, hash $rv) {
        }

        #! default implementation is empty
        logInfo(string $fmt) {
            delete $argv;
        }

        #! default implementation is empty
        logError(string $fmt) {
            delete $argv;
        }

        #! default implementation is empty
        logDebug(string $fmt) {
            delete $argv;
        }

        #! adds a REST class to the handler
        addClass(AbstractRestClass $cls) {
            $.mx.lock();
            on_exit $.mx.unlock();

            my string $name = $cls.name();
            if ($.ch{$name})
                throw "RETSHANDLER-ERROR", sprintf("class %y has already been registered with this handler", $name);
            $.ch{$name} = $cls;
        }

        #! creates a hash for an HTTP response with the response code and the response message body as a formatted string
        static hash makeResponse(int $code, string $fmt) {
            return (
                "code": $code,
                "body": vsprintf($fmt, $argv),
                );
        }

        #! creates a hash for an HTTP response with the response code and a literal response message body
        static hash makeResponse(int $code, any $body) {
            return (
                "code": $code,
                "body": $body,
                );
        }

        #! creates a hash for an HTTP 400 error response with the response message body as a string
        static hash make400(string $fmt) {
            return RestHandler::makeResponse(400, vsprintf($fmt, $argv));
        }

        #! creates a hash for an HTTP 501 error response with the response message body as a string
        static hash make501(string $fmt) {
            return RestHandler::makeResponse(501, vsprintf($fmt, $argv));
        }
    }
}
