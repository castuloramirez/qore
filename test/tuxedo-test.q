#! /usr/bin/env qore

# A test suite for Tuxedo integration.
# Most of the tests will require appropriate Tuxed server started, 
# some use "dummy" mode without the need for Tuxedo.
#
# -------------------------------------------------------------------
#  Usage:
#    tuxedo-test.q           # runs all tests
#    tuxedo-test.q N         # N is integer, runs only the test N
#    tuxedo-test.q debug     # run all "dummy" tests 
#                            # which do not require Tuxedo servers running.
#                            # Qore must be compiled in DEBUG mode.
#
# -------------------------------------------------------------------
# 
# The Tuxedo::Connection allows to specify all needed settings,
# without the need to set environment variables.
# However, it may be desirable to use environment variables
# in some cases. For details see 
# http://edocs.bea.com/tuxedo/tux91/rf3c/rf3c55.htm#1022852
#
# Those variables may be needed:
#  * TUXDIR - directory where Tuxedo is installed
#      (e.g. /opt/bea/tuxedo9.1)
#  * TUXCONFIG - location of binary configuration file,
#      generated by tploadcf
#      (e.g. /home/joe/my_app/tuxconfig)
#  * MACHINE_NAME - result of 'uname' command, may be 
#      needed in some situations, (e.g. astra)
#  * PATH and LD_LIBRARY_PATH should be set up to find
#      necessary components of Tuxedo
#      (e.g. LD_LIBRARY_PATH may be /opt/bea/tuxedo9.1/lib)
#
# -------------------------------------------------------------------

%requires tuxedo

# change these as needed
$TUXDIR = "/opt/bea/tuxedo9.1";
$TUXCONFIG_BASE = `/bin/echo -n $HOME` + "/tuxedo_tests/";

# ------------------------------------------------------------------
# dummy test
sub test000d()
{
}

# -------------------------------------------------------------------
# dummy connection, should not throw
sub test001d()
{
  $conn = new Tuxedo::TuxedoConnection("test");
}

# -------------------------------------------------------------------
# dummy connection, should throw in constructor
sub test002d()
{
  try 
    $conn = new Tuxedo::TuxedoConnection("test-fail-open");
  catch() 
    return;
  printf("ERROR: test002d() fails - exception was expected.\n");
  exit(1);
  
}

# -------------------------------------------------------------------
# dummy connection, should throw in destructor
sub test003d()
{
  $conn = new Tuxedo::TuxedoConnection("test-fail-close");
  try
    delete $conn;
  catch()
    return;
  printf("ERROR: test003d() fails - exception was expected.\n");
  exit(1);
}

# -------------------------------------------------------------------
# dummy connection, copy constructor should throw
sub test004d()
{
  $conn = new Tuxedo::TuxedoConnection("test");
  try 
    $conn2 = $conn.copy();
  catch()
    return;
  printf("ERROR: test004d() fails - exception was expected.\n");
  exit(1);
}

# -------------------------------------------------------------------
# real connection, non-existent service, should throw even
# environment points to valid running service
sub test005d()
{
  try 
    $conn = new Tuxedo::TuxedoConnection("nonexistent",
      ( "TUXDIR" : $TUXDIR,
        "TUXCONFIG" : "/nonexistent/tuxdir" )); # <- invalid config file
  catch ()
    return;
  printf("ERROR: test005d() fails - exception was expected.\n");
  exit(1);
}

# -------------------------------------------------------------------
# Create connection to a real service identified only by environment
# variables (at least TUXDIR and TUXCONFIG). The service must be running.
sub test006()
{
  $conn = new Tuxedo::TuxedoConnection("running service identified by env");
}

# ------------------------------------------------------------------
# Create connection to the service identified by parameters.
# The service must be running from given directory,
# it may be the simplest sample (simpapp) or whatever else.
sub test007()
{
  $conn = new Tuxedo::TuxedoConnection("a service",
    ( "TUXDIR" : $TUXDIR,
      "TUXCONFIG" : $TUXCONFIG_BASE + "tuxedo_simple_app/tuxconfig" ));
}

# -------------------------------------------------------------------
# Dummy adapter (no need for connection in such cases).
sub test008d()
{
  $adapter = new Tuxedo::TuxedoAdapter("test");
}

# -------------------------------------------------------------------
# Dummy adapter throwing in constructor
sub test009d()
{
  try 
    $adapter = new Tuxedo::TuxedoAdapter("test-fail-open");
  catch () 
    return;
  printf("ERROR: test009d() fails - exception was expected.\n");
  exit(1);
}

# -------------------------------------------------------------------
# Dummy adapter throwing in destructor
sub test010d()
{
  $adapter = new Tuxedo::TuxedoAdapter("test-fail-close");
  try 
    delete $adapter;
  catch()
    return;
  printf("ERROR: test010d() fails - exception was expected.\n");
  exit(1);
}

# -------------------------------------------------------------------
# Create connection to a real service identified only by environment
# variables (at least TUXDIR and TUXCONFIG). The service must be running.
# Create also an adapter.
sub test011()
{
  $conn = new Tuxedo::TuxedoConnection("running service identified by env");
  $adapter = new Tuxedo::TuxedoAdapter("an adapter", Tuxedo::TPNOTRAN);
}

# -------------------------------------------------------------------
# Dummy adapter, async call that fails.
sub test012d()
{
  $adapter = new Tuxedo::TuxedoAdapter("test-fail-call");
  try 
    $handle = $adapter.async_call();
  catch()
    return;
  printf("ERROR: test012d() fails - exception is expected.\n");
  exit(1);
}

# -------------------------------------------------------------------
# Dummy adapter, async call that is cancelled
sub test013d()
{
  $adapter = new Tuxedo::TuxedoAdapter("test");
  $handle = $adapter.async_call();
  if ($handle != 0) {
    printf("ERROR in test013d() - expected handle == 0.\n");
    exit(1);
  }
}

# -------------------------------------------------------------------
# Dummy adapter, cancelling of async call fails.
sub test014d()
{
  $adapter = new Tuxedo::TuxedoAdapter("test-fail-cancel");
  $handle = $adapter.async_call();
  if ($handle != 0) {
    printf("ERROR in test014d() - expected handle == 0.\n");
    exit(1);
  }
  try 
    $adapter.cancel_async($handle);
  catch()
    return;
  printf("ERROR in test014d() - exception is expected.\n");
  exit(1);
}

# -------------------------------------------------------------------
# Dummy adapter that fails to get result.
sub test015d()
{
  $adapter = new Tuxedo::TuxedoAdapter("test-fail");
  $handle = $adapter.async_call();
  if ($handle != 0) {
    printf("ERROR in test015d() - expected handle == 0.\n");
    exit(1);
  }
  try
    $res = $adapter.get_async_result($handle);
  catch()
    return;
  printf("ERROR: test015d() fails - exception was expected.\n");
  exit(1);
}

# -------------------------------------------------------------------
# Dummy adapter, fails copying.
sub test016d()
{
  $adapter = new Tuxedo::TuxedoAdapter("test");
  try
   $copy = $adapter.copy();
  catch()
    return;
  printf("ERROR in test016d() - exception was expected.\n");
  exit(1);
}

# -------------------------------------------------------------------
# Dummy adapter, async call returns int.
sub test017d()
{
  $adapter = new Tuxedo::TuxedoAdapter("test-success-int");
  $handle = $adapter.async_call();
  if ($handle != 0) {
    printf("ERROR in test017d() - expected handle == 0.\n");
    exit(1);
  }
  $res = $adapter.get_async_result($handle);
  if ($res != 123) {
    printf("ERROR in test017d() - value 123 expected.\n");
    exit(1);
  }
}

# -------------------------------------------------------------------
# Dummy adapter, async call returns bool.
sub test018d()
{
  $adapter = new Tuxedo::TuxedoAdapter("test-success-bool");
  $handle = $adapter.async_call();
  if ($handle != 0) {
    printf("ERROR in test018d() - expected handle == 0.\n");
    exit(1);
  }
  $res = $adapter.get_async_result($handle);
  if ($res != True) {
    printf("ERROR in test018d() - value True expected.\n");
    exit(1);
  }
}

# -------------------------------------------------------------------
# Dummy adapter, async call returns string.
sub test019d()
{
  $adapter = new Tuxedo::TuxedoAdapter("test-success-string");
  $handle = $adapter.async_call();
  if ($handle != 0) {
    printf("ERROR in test019d() - expected handle == 0.\n");
    exit(1);
  }
  $res = $adapter.get_async_result($handle);
  if ($res != "test result") {
    printf("ERROR in test019d() - value \"test result\"  expected.\n");
    exit(1);
  }
}

# -------------------------------------------------------------------
# Calls "TOUPPER" service provided by simpapp ATMI sample
sub test020()
{
  $conn = new Tuxedo::TuxedoConnection("a service",
    ( "TUXDIR" : $TUXDIR,
      "TUXCONFIG" : $TUXCONFIG_BASE + "tuxedo_simple_app/tuxconfig" ));

  $adapter = new Tuxedo::TuxedoAdapter("TOUPPER service");

  $list = ("string", "test data");
  $result = $adapter.call("TOUPPER", $list, Tuxedo::TPNOTRAN);

  if ($result[0] != "string") {
    printf("ERROR in test020() - invalid data type returned.\n");
    exit(1);
  }
  if ($result[1] != "TEST DATA") {
    printf("ERROR in test020() - invalid result returned.\n");
  }
}

# -------------------------------------------------------------------
# The same as test020 but starting as async call and then canceling.
sub test021()
{
  $conn = new Tuxedo::TuxedoConnection("a service",
    ( "TUXDIR" : $TUXDIR,
      "TUXCONFIG" : $TUXCONFIG_BASE + "tuxedo_simple_app/tuxconfig" ));

  $adapter = new Tuxedo::TuxedoAdapter("TOUPPER service");

  $list = ("string", "test data");
  $handle = $adapter.async_call("TOUPPER", $list, 0);
  $adapter.cancel_async($handle);
}

# -------------------------------------------------------------------
# The same as in test020() but using asynchronous call.
sub test022()
{
  $conn = new Tuxedo::TuxedoConnection("a service",
    ( "TUXDIR" : $TUXDIR,
      "TUXCONFIG" : $TUXCONFIG_BASE + "tuxedo_simple_app/tuxconfig" ));

  $adapter = new Tuxedo::TuxedoAdapter("TOUPPER service");

  $list = ("string", "test data");
  $handle = $adapter.async_call("TOUPPER", $list, 0);

  $result = $adapter.get_async_result($handle, 0);

  if ($result[0] != "string") {
    printf("ERROR in test022() - invalid data type returned.\n");
    exit(1);
  }
  if ($result[1] != "TEST DATA") {
    printf("ERROR in test022() - invalid result returned.\n");
  }
}

# -------------------------------------------------------------------
sub run_all()
{
  test000d();
  test001d();
  test002d();
  test003d();
  test004d();
  test005d();
  test006();
  test007();
  test008d();
  test009d();
  test010d();
  test011();
  test012d();
  test013d();
  test014d();
  test015d();
  test016d();
  test017d();
  test018d();
  test019d();
  test020();
  test021();
  test022();
}

# -------------------------------------------------------------------
sub run_debug()
{
  test000d();
  test001d();
  test002d();
  test003d();
  test004d();
  test005d();
  test008d();
  test009d();
  test010d();
  test012d();
  test013d();
  test014d();
  test015d();
  test016d();
  test017d();
  test018d();
  test019d();
}

# -------------------------------------------------------------------
sub run_single_test($N)
{
  switch ($N) {
  case "0": test000d(); break;
  case "1": test001d(); break;
  case "2": test002d(); break;
  case "3": test003d(); break;
  case "4": test004d(); break;
  case "5": test005d(); break;
  case "6": test006(); break;
  case "7": test007(); break;
  case "8": test008d(); break;
  case "9": test009d(); break;
  case "10": test010d(); break;
  case "11": test011(); break;
  case "12": test012d(); break;
  case "13": test013d(); break;
  case "14": test014d(); break;
  case "15": test015d(); break;
  case "16": test016d(); break;
  case "17": test017d(); break;
  case "18": test018d(); break;
  case "19": test019d(); break;
  case "20": test020(); break;
  case "21": test021(); break;
  case "22": test022(); break;
  default: printf("ERROR: function [%s] not found.\n", $N);
  }
}

# -------------------------------------------------------------------
sub main()
{
  try  {

  if (elements $ARGV == 0) {
    printf("All test will be run.\n");
    run_all();
  } else
  if (elements $ARGV != 1) {
    printf("Usage: tuxedo_test.q [{debug|N}]\n");
  } else {
    if ($ARGV[0] == "debug") {
      printf("Running debug tests.\n");
      run_debug();
    } else {
      printf("Running single test.\n");
      run_single_test($ARGV[0]);      
    }
  }

  printf("Finished\n");

  } catch () {
   printf("\n\n");
   rethrow;
  }
}

main();

# EOF

